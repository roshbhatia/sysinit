#!/usr/bin/env zsh
# THIS FILE WAS INSTALLED BY SYSINIT. MODIFICATIONS WILL BE OVERWRITTEN UPON UPDATE.
# shellcheck disable=all

path_add() {
  if [ -d "$1" ] && [[ ":$PATH:" != *":$1:"* ]]; then
    PATH="${PATH:+"$PATH:"}$1"
  fi
}

path_add "/opt/homebrew/bin"
path_add "/usr/local/bin"
path_add "$HOME/.cargo/bin"
path_add "/etc/profiles/per-user/$USER/bin"

if [[ $# -ne 1 ]]; then
  >&2 echo "usage: $0 FILENAME[:LINENO][:IGNORED]"
  exit 1
fi

target="${1/#\~\/$HOME/}"
center=0

if [[ ! -r "$target" ]]; then
  if [[ "$target" =~ ^(.+):([0-9]+)\ *$ ]] && [[ -r "${match[1]}" ]]; then
    target="${match[1]}"
    center="${match[2]}"
  elif [[ "$target" =~ ^(.+):([0-9]+):[0-9]+\ *$ ]] && [[ -r "${match[1]}" ]]; then
    target="${match[1]}"
    center="${match[2]}"
  fi
fi

if [[ ! -r "$target" ]]; then
  file "$1"
  exit 1
fi

type=$(file --brief --dereference --mime -- "$target")

if [[ -d "$target" ]]; then
  eza --color=always --icons=always -1 "$target"
elif [[ "$type" =~ ^image/ ]]; then
  if command -v wezterm &>/dev/null; then
    wezterm imgcat "$target"
  else
    file "$target"
  fi
elif [[ "$type" =~ =binary ]]; then
  file "$target"
else
  local batname
  if command -v batcat &>/dev/null; then
    batname="batcat"
  elif command -v bat &>/dev/null; then
    batname="bat"
  else
    cat "$target"
    exit
  fi

  if [[ "$center" -gt 0 ]]; then
    "${batname}" --style=numbers --color=always --pager=never --highlight-line="$center" -- "$target"
  else
    "${batname}" --style=numbers --color=always --pager=never -- "$target"
  fi
fi

